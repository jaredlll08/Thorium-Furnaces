import org.jetbrains.gradle.ext.JarApplication

plugins {
    id 'org.jetbrains.gradle.plugin.idea-ext' version '1.3'
    id 'base'
    id 'maven-publish'
}

ext {
    hytaleHome = "${System.getProperty("user.home")}/AppData/Roaming/Hytale"
}

version += "." + (System.getenv('BUILD_NUMBER') ? System.getenv('BUILD_NUMBER') : "0")

// Create the working directory to run the server if it does not already exist.
def serverRunDir = file("$projectDir/run")
if (!serverRunDir.exists()) {
    serverRunDir.mkdirs()
}

// Updates the manifest.json file with the latest properties defined in the
// build.properties file. Currently we update the version and if packs are
// included with the plugin.
tasks.register('updatePluginManifest') {
    def manifestFile = file('src/main/resources/manifest.json')
    doLast {
        if (!manifestFile.exists()) {
            throw new GradleException("Could not find manifest.json at ${manifestFile.path}!")
        }
        def manifestJson = new groovy.json.JsonSlurper().parseText(manifestFile.text)
        manifestJson.Group = project.manifest_group
        manifestJson.Name = project.manifest_name
        manifestJson.Version = version
        manifestFile.text = groovy.json.JsonOutput.prettyPrint(groovy.json.JsonOutput.toJson(manifestJson))
    }
}

// Builds a pack into the build/distributions folder
tasks.register('buildPack', Zip) {
    dependsOn 'updatePluginManifest'
    from fileTree(dir: "${project.projectDir}/src/main/resources")
    excludes = ['**/*.psd']
    archiveFileName = "${project.name}-${project.version}.zip"
}
// Call buildPack when building
tasks.named('build') {
    dependsOn 'buildPack'
}

// Creates a run configuration in IDEA that will run the Hytale server with
// your plugin and the default assets.
idea.project.settings.runConfigurations {
    'HytaleServer'(org.jetbrains.gradle.ext.JarApplication) {
        jarPath = file("$hytaleHome/install/$patchline/package/game/latest/Server/HytaleServer.jar").absolutePath
        moduleName = project.idea.module.name + '.main'
        programParameters = "--allow-op --assets=$hytaleHome/install/$patchline/package/game/latest/Assets.zip"
        programParameters += " --mods=${file('src/main').absolutePath}"
        if (load_user_mods.toBoolean()) {
            programParameters += " --mods=$hytaleHome/UserData/Mods"
        }
        workingDirectory = serverRunDir.absolutePath
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact(tasks.named('buildPack'))
        }
    }

    repositories {
        maven {
            url "file://" + System.getenv("local_maven")
        }
    }
}